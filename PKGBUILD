# Maintainer: Sibren Vasse <arch@sibrenvasse.nl>
# Contributor: Ilya Gulya <ilyagulya@gmail.com>
pkgname="deezer"
pkgver=7.1.20
pkgrel=1
_mpris_ver=2.1.5
pkgdesc="A proprietary music streaming service"
arch=('any')
url="https://www.deezer.com/"
license=('custom')
depends=('electron38' 'hicolor-icon-theme')
provides=('deezer')
makedepends=('p7zip' 'asar' 'imagemagick' 'npm')
source=("$pkgname-$pkgver-setup.exe::https://www.deezer.com/desktop/download/artifact-win32-x86-$pkgver"
    "$pkgname-desktop.desktop"
    "deezer-desktop"
    "prettierrc.json"
    "LICENSE"
    "mpris-service-${_mpris_ver}.tgz::https://registry.npmjs.org/@jellybrick/mpris-service/-/mpris-service-${_mpris_ver}.tgz"
    # Upstream patches from aunetx/deezer-linux
    "01-start-in-tray.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/01-start-in-tray.patch"
    "02-start-without-tray.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/02-start-without-tray.patch"
    "03-disable-auto-updater.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/03-disable-auto-updater.patch"
    "04-remove-os-information.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/04-remove-os-information.patch"
    "05-provide-metadata-mpris.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/05-provide-metadata-mpris.patch"
    "06-control-log-level.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/06-control-log-level.patch"
    "07-improve-responsiveness.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/07-improve-responsiveness.patch"
    "08-hide-offline-banner.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/08-hide-offline-banner.patch"
    "09-disable-animations.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/09-disable-animations.patch"
    "10-disable-notifications.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/10-disable-notifications.patch"
    "11-fix-thumbar-actions.patch::https://raw.githubusercontent.com/aunetx/deezer-linux/master/patches/11-fix-thumbar-actions.patch"
    # Local patches
    "99-systray-icon.patch"
)
sha256sums=('48ab73820fe92b66bf610aba82a0df907a089e5028426c2ee8125486fbc9dbee'
            '41a32f7e595a52c260aa872e8273430d83e4c5fcc8e529e21afa4dab3d6b1d9c'
            '14b4b2b553b66e2cf0e45bc7274679dd06bdb891f04f2aa3be7afa96eb84101e'
            '951c80c9dc4848b31a5fcc9655b005f3a218b1ff735396168c13922ac658c83b'
            'fe9bab50ef433516105ed2f2c349736c0be87c2f75f8d18bf1f011b2115019c1'
            '181016c614fc62595d604d336cdb43c40645d7cc66a35acaf562c9af26177a6e'
            '0d0034a367fb4e70849b604a2ab35d5ced0dccb67ad16c231783acfc9b8f64ee'
            'a369f25a29b43112cb80873d8e1691a66ea1273fd13507d1e698223bb6233a2b'
            '77eee637ad6f2571169a72f2dda125e004f4ed8471e4de71d7d0175772600cde'
            'f8ea3dd7c788c79d77d656dc9b3b8d5b8891d88f3bdccbc3d9fb3bd95cec9f9c'
            'fe0504f6e77f12bb09a5b14ecdbebe743a0bf576d8b10d9e891c53e257d1cc14'
            'a95a35315d00c353be2aae8cacaab22283377bb0d3177a22c18c220b8f5ece69'
            'd1fe858c87e1d8f36c16b8ce185052e182196e06174f27f9ce5112762282d3d8'
            '81543bb13c0977890c634f8ac150a451a509fc5acecaf1ab6a2abfa50493617d'
            '70daf38a2c14b13e7e1f374ff4ab9220f4e356148eeaf5d9b08b104aaaed0e91'
            'ab25994396bc15cd375ee243c3adffee62202381f45808ceee8a67c149f82fb1'
            '6681e628769fe2267a976d25f07036fe8e6dbc4f59b301d9d7533848fc53dc7b'
            '2362c52147cf67759388ba6685e2add32c5bcfcdbdbca8e0990f58cafa0392de')

prepare() {
    # Extract app from installer
    7z x -so $pkgname-$pkgver-setup.exe "\$PLUGINSDIR/app-32.7z" >app-32.7z
    # Extract app archive
    7z x -y -bsp0 -bso0 app-32.7z

    # Extract png from ico container
    magick resources/win/app.ico resources/win/deezer-%d.png

    # Install Prettier in the build root
    npm install --no-save --prefix "$srcdir" prettier@3.5.3

    cd resources/
    asar extract app.asar app

    cd "$srcdir/resources/app"
    mkdir -p resources/linux/
    for size in 24 48; do
	    magick "$srcdir/resources/win/deezer-8.png" -resize "${size}x${size}" -strip \
            -define png:compression-filter=5 -define png:compression-level=9 \
            "resources/linux/systray-${size}.png"
    done

    cp "$srcdir/prettierrc.json" .prettierrc
    "$srcdir/node_modules/.bin/prettier" --write "build/*.{js,html}"

    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch ${src}..."
      patch -Np1 -l -F3 < "${srcdir}/${src}"
    done

    # Install mpris from verified local tarball
    npm install --no-save "$srcdir/mpris-service-${_mpris_ver}.tgz"

    cd "$srcdir/resources/"
    asar pack app app.asar
}

package() {
    mkdir -p "$pkgdir/usr/share/deezer"
    mkdir -p "$pkgdir/usr/share/applications"
    mkdir -p "$pkgdir/usr/bin/"
    for size in 16 22 24 32 48 64 128 256 512; do
        install -d "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/"
        magick resources/win/deezer-8.png -resize "${size}x${size}" -strip \
            -define png:compression-filter=5 -define png:compression-level=9 \
            "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/deezer-desktop.png"
    done

    install -Dm644 resources/app.asar "$pkgdir/usr/share/deezer/"
    install -Dm644 "$pkgname-desktop.desktop" "$pkgdir/usr/share/applications/"
    install -Dm755 deezer-desktop "$pkgdir/usr/bin/"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
